import ./setup as setup
from bolt_expressions import Scoreboard, Data
from ./scalevar import ScaleVar
import math

# Credit: Triton360 https://github.com/Triton365/fast_integer_sqrt/blob/main/functions/isqrt_noreturn.mcfunction
#   - Returns the square root of the input score as a bolt_expr scoreboard
#   - This is basically just taking Triton's code and wrapping it in a convenient bolt function
def sqrt(x):
    if isinstance(x, ScaleVar):
        sqrt_in = ScaleVar.score("#x", "kmath", x.scale)
        sqrt_out = ScaleVar.score("#y", "kmath", x.scale)

        sqrt_in = x * x.scale
        function ./sqrt
        return sqrt_out
    else:
        return math.sqrt(x)

# Function definition from Triton360
function ./sqrt:
    execute store result score #t2 kmath run scoreboard players operation #t1 kmath = #x kmath
    execute if score #x kmath matches 0..19310:
        scoreboard players add #t1 kmath 4095
        scoreboard players set #y kmath -594039
        scoreboard players operation #y kmath /= #t1 kmath
        scoreboard players add #y kmath 149
    
    execute if score #x kmath matches 19311..1705544:
        scoreboard players add #t1 kmath 903167
        scoreboard players set #y kmath -1568669636
        scoreboard players operation #y kmath /= #t1 kmath
        scoreboard players add #y kmath 1857
    
    execute if score #x kmath matches 1705545..39400514:
        scoreboard players operation #t1 kmath /= #141 kmath
        scoreboard players add #t1 kmath 224767
        scoreboard players set #y kmath -2147483648
        scoreboard players operation #y kmath /= #t1 kmath
        scoreboard players add #y kmath 10425
    
    execute if score #x kmath matches 39400515..455779650:
        scoreboard players operation #t1 kmath /= #7775 kmath
        scoreboard players add #t1 kmath 60419
        scoreboard players set #y kmath -2147483648
        scoreboard players operation #y kmath /= #t1 kmath
        scoreboard players add #y kmath 39184
    
    execute if score #x kmath matches 455779651..2147483647:
        scoreboard players operation #t1 kmath /= #100000 kmath
        scoreboard players add #t1 kmath 26111
        scoreboard players set #y kmath -2147483648
        scoreboard players operation #y kmath /= #t1 kmath
        scoreboard players add #y kmath 91181
    
    scoreboard players operation #t2 kmath /= #y kmath
    scoreboard players operation #y kmath += #t2 kmath
    scoreboard players operation #y kmath /= #2 kmath
    scoreboard players operation #x kmath /= #y kmath
    execute if score #y kmath > #x kmath run scoreboard players remove #y kmath 1